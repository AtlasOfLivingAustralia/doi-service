buildscript {
    repositories {
        mavenLocal()
        maven { url "https://nexus.ala.org.au/content/groups/public/" }
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "org.grails.plugins:hibernate5:${gormVersion}"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:3.4.6"
        classpath "gradle.plugin.com.github.erdi.webdriver-binaries:webdriver-binaries-gradle-plugin:$webdriverBinariesVersion"
        classpath 'org.grails.plugins:database-migration:4.2.0'
    }
}

plugins {
    id "com.gorylenko.gradle-git-properties" version "2.4.1"
}

version "3.1.0-SNAPSHOT"
group "au.org.ala"

apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"war"
apply plugin:"org.grails.grails-web"
apply plugin:"com.github.erdi.webdriver-binaries"
apply plugin:"com.bertramlabs.asset-pipeline"
apply plugin:"org.grails.grails-gsp"
apply plugin:"maven-publish"

sourceCompatibility = 1.11
targetCompatibility = 1.11

repositories {
    mavenLocal()
    maven { url "https://nexus.ala.org.au/content/groups/public/" }
    maven { url "https://repo.grails.org/grails/core" }
    maven { url "https://repository.gbif.org/content/groups/gbif/" }
    mavenCentral()
}

// Inplace plugin config
// Set to true it needed only for development
boolean inplace = false

if(inplace) {
    grails {
        exploded = true
        plugins {
//            compile project(':ala-admin-plugin')
            implementation project(':ala-auth-plugin')
        }
    }
}

def elasticsearchVersion = '7.3.0'
ext['elasticsearch.version'] = elasticsearchVersion
// To prevent: http://hg.openjdk.java.net/jdk8/jdk8/jaxp/rev/5c84d4a878f1
// when using gbif-doi datacite xml validator
def withoutXerces = { exclude group: 'xerces', module: 'xercesImpl' }

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    implementation {
        exclude group: "xerces", module: "xercesImpl"
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    developmentOnly("org.springframework.boot:spring-boot-devtools")
    implementation "org.springframework.boot:spring-boot-starter-logging"
    implementation "org.springframework.boot:spring-boot-autoconfigure"
    implementation "org.grails:grails-core"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-tomcat"
    implementation "org.grails:grails-web-boot"
    implementation "org.grails:grails-logging"
    implementation "org.grails:grails-plugin-rest"
    implementation "org.grails:grails-plugin-databinding"
    implementation "org.grails:grails-plugin-i18n"
    implementation "org.grails:grails-plugin-services"
    implementation "org.grails:grails-plugin-url-mappings"
    implementation "org.grails:grails-plugin-interceptors"
    implementation "org.grails.plugins:cache"
    implementation "org.grails.plugins:cache-ehcache:3.0.0"
    implementation "org.grails.plugins:async"
    implementation "org.grails.plugins:scaffolding"
    implementation "org.grails.plugins:events"
    implementation "org.grails.plugins:hibernate5"
    implementation "org.hibernate:hibernate-jcache"
    implementation "org.codehaus.groovy.modules.http-builder:http-builder:0.7.2"
    runtimeOnly 'org.ehcache:ehcache'

    implementation "org.grails.plugins:gsp"
    compileOnly "io.micronaut:micronaut-inject-groovy"
    console "org.grails:grails-console"
    profile "org.grails.profiles:web"
    runtimeOnly "org.glassfish.web:el-impl:2.1.2-b03"
    runtimeOnly "javax.xml.bind:jaxb-api:2.3.1"
    runtimeOnly "com.bertramlabs.plugins:asset-pipeline-grails:3.4.6"
    testImplementation "io.micronaut:micronaut-inject-groovy"
    testImplementation "org.grails:grails-gorm-testing-support"
    testImplementation "org.mockito:mockito-core"
    testImplementation "org.grails:grails-web-testing-support"
    testImplementation "org.grails.plugins:geb"
    testImplementation "org.seleniumhq.selenium:selenium-remote-driver:$seleniumVersion"
    testImplementation "org.seleniumhq.selenium:selenium-api:$seleniumVersion"
    testImplementation "org.seleniumhq.selenium:selenium-support:$seleniumVersion"
    testImplementation "org.seleniumhq.selenium:selenium-chrome-driver:$seleniumVersion"
    testImplementation "org.seleniumhq.selenium:selenium-firefox-driver:$seleniumVersion"

    implementation "org.grails.plugins:converters"
    runtimeOnly 'org.codehaus.groovy:groovy-dateutil'
    implementation 'dk.glasius:external-config:3.1.1'

    implementation 'org.grails.plugins:elasticsearch:3.0.0-RC1'

    implementation group: 'org.grails.plugins', name: 'ala-bootstrap3', version: '4.1.0', changing: true
    implementation group: 'org.grails.plugins', name: 'ala-admin-plugin', version: '2.3.0', changing: true

    implementation "org.grails.plugins:ala-auth:$alaSecurityLibsVersion"
    implementation "org.grails.plugins:ala-ws-plugin:$alaSecurityLibsVersion"
    implementation "org.grails.plugins:ala-ws-security-plugin:$alaSecurityLibsVersion"


    implementation 'org.grails.plugins:quartz:2.0.13'
    implementation 'org.quartz-scheduler:quartz:2.3.2' // Is not pulled in by default https://stackoverflow.com/questions/61144025/quartz-not-working-in-grails4-issue-in-dependency-not-able-to-compile

    implementation "org.grails.plugins:mail:3.0.0"
    implementation 'au.org.ala.plugins:openapi:1.1.0'

    implementation "org.gbif:gbif-doi:2.21"

    // override log4j in elasticsearch plugin
    // TODO remove this once no log4j vulnerable transitive deps are used.
    runtimeOnly 'org.apache.logging.log4j:log4j-core:2.17.1'
    runtimeOnly 'org.apache.logging.log4j:log4j-api:2.17.1'

    // DB related
    implementation group: 'org.postgresql', name: 'postgresql', version: '42.3.3'
    implementation 'org.grails.plugins:postgresql-extensions:7.0.0'
    implementation 'org.flywaydb:flyway-core:8.5.2'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.2'

    implementation group: 'com.google.guava', name: 'guava', version: '23.2-jre'
    implementation 'com.jakewharton.byteunits:byteunits:0.9.1'

    implementation 'org.grails.plugins:aws-sdk-s3:2.4.8'
    implementation 'org.javaswift:joss:0.10.4'
    implementation 'org.jsoup:jsoup:1.11.3'


    testImplementation "org.apache.httpcomponents:httpmime:4.3.3"
}

springBoot {
    buildInfo()
//    layout = 'WAR'
}

bootRun {
    ignoreExitValue true
    jvmArgs(
            '-Dspring.output.ansi.enabled=always',
            '-noverify',
            '-XX:TieredStopAtLevel=1',
            '-Xmx1024m')
    sourceResources sourceSets.main
    String springProfilesActive = 'spring.profiles.active'
    systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}

bootWar {
    launchScript()
    classifier = 'exec'
}

tasks.withType(GroovyCompile) {
    configure(groovyOptions) {
        forkOptions.jvmArgs = ['-Xmx1024m']
    }
}

tasks.withType(Test) {
    systemProperty "geb.env", System.getProperty('geb.env')
    systemProperty "geb.build.reportsDir", reporting.file("geb/integrationTest")
    systemProperty "webdriver.chrome.driver", System.getProperty('webdriver.chrome.driver')
    systemProperty "webdriver.gecko.driver", System.getProperty('webdriver.gecko.driver')
}

assets {
    minifyJs = true
    minifyCss = true
//    includes = ["fonts/*"]
}


publishing {
    targetCompatibility = 1.11
    repositories {
        maven {
            name 'Nexus'
            url "https://nexus.ala.org.au/content/repositories/${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'}"
            credentials {
                username = System.getenv('TRAVIS_DEPLOY_USERNAME')
                password = System.getenv('TRAVIS_DEPLOY_PASSWORD')
            }
        }
    }
    publications {
        mavenJar(MavenPublication) {
            artifact bootWar
        }
    }
}
