buildscript {
    repositories {
        mavenLocal()
        maven { url "https://nexus.ala.org.au/content/groups/public/" }
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "gradle.plugin.com.github.erdi.webdriver-binaries:webdriver-binaries-gradle-plugin:2.0"
        classpath "org.grails.plugins:hibernate5:7.0.5"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:3.4.3"
    }
}

plugins {
    id "com.gorylenko.gradle-git-properties" version "2.3.2"
}

version "2.0.0-SNAPSHOT"
group "au.org.ala"

apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"war"
apply plugin:"org.grails.grails-web"
apply plugin:"com.github.erdi.webdriver-binaries"
apply plugin:"com.bertramlabs.asset-pipeline"
apply plugin:"org.grails.grails-gsp"
apply plugin:"maven-publish"

sourceCompatibility = 1.11
targetCompatibility = 1.11

repositories {
    mavenLocal()
    maven { url "https://nexus.ala.org.au/content/groups/public/" }
    maven { url "https://repo.grails.org/grails/core" }
    maven { url "https://repository.gbif.org/content/groups/gbif/" }
    mavenCentral()
}

// Inplace plugin config
// Set to true it needed only for development
boolean inplace = false

if(inplace) {
    grails {
        exploded = true
        plugins {
//            compile project(':ala-admin-plugin')
            compile project(':ala-auth-plugin')
        }
    }
}

def elasticsearchVersion = '7.3.0'
ext['elasticsearch.version'] = elasticsearchVersion
// To prevent: http://hg.openjdk.java.net/jdk8/jdk8/jaxp/rev/5c84d4a878f1
// when using gbif-doi datacite xml validator
def withoutXerces = { exclude group: 'xerces', module: 'xercesImpl' }

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    developmentOnly("org.springframework.boot:spring-boot-devtools")
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.grails:grails-core"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-tomcat"
    compile "org.grails:grails-web-boot"
    compile "org.grails:grails-logging"
    compile "org.grails:grails-plugin-rest"
    compile "org.grails:grails-plugin-databinding"
    compile "org.grails:grails-plugin-i18n"
    compile "org.grails:grails-plugin-services"
    compile "org.grails:grails-plugin-url-mappings"
    compile "org.grails:grails-plugin-interceptors"
    compile "org.grails.plugins:cache"
    compile "org.grails.plugins:cache-ehcache:3.0.0"
    compile "org.grails.plugins:async"
    compile "org.grails.plugins:scaffolding"
    compile "org.grails.plugins:events"
    compile "org.grails.plugins:hibernate5"
//    compile "org.hibernate:hibernate-core:5.4.18.Final"
    compile "org.hibernate:hibernate-jcache"
    runtime 'org.ehcache:ehcache'

    compile "org.grails.plugins:gsp"
    compileOnly "io.micronaut:micronaut-inject-groovy"
    console "org.grails:grails-console"
    profile "org.grails.profiles:web"
    runtime "org.glassfish.web:el-impl:2.1.2-b03"
//    runtime "com.h2database:h2"
//    runtime "org.apache.tomcat:tomcat-jdbc"
    runtime "javax.xml.bind:jaxb-api:2.3.1"
    runtime "com.bertramlabs.plugins:asset-pipeline-grails:3.2.4"
    testCompile "io.micronaut:micronaut-inject-groovy"
    testCompile "org.grails:grails-gorm-testing-support"
    testCompile "org.mockito:mockito-core"
    testCompile "org.grails:grails-web-testing-support"
    testCompile "org.grails.plugins:geb"
    testCompile "org.seleniumhq.selenium:selenium-remote-driver:3.14.0"
    testCompile "org.seleniumhq.selenium:selenium-api:3.14.0"
    testCompile "org.seleniumhq.selenium:selenium-support:3.14.0"
    testRuntime "org.seleniumhq.selenium:selenium-chrome-driver:3.14.0"
    testRuntime "org.seleniumhq.selenium:selenium-firefox-driver:3.14.0"

    compile "org.grails.plugins:converters"
    runtime 'org.codehaus.groovy:groovy-dateutil'
    compile 'dk.glasius:external-config:2.0.1'

    compile 'org.grails.plugins:elasticsearch:3.0.0-RC1'

    compile group: 'org.grails.plugins', name: 'ala-ws-plugin', version: '3.1.1', changing: true, withoutXerces
    compile group: 'org.grails.plugins', name: 'ala-bootstrap3', version: '4.1.0', changing: true
    compile group: 'org.grails.plugins', name: 'ala-admin-plugin', version: '2.3.0', changing: true
    compile group: 'org.grails.plugins', name: 'ala-auth', version: '5.1.1', changing: true
    compile group: 'org.grails.plugins', name: 'ala-ws-security-plugin', version: '4.1.1', changing: true

//    compile "org.grails.plugins:ala-ws-security-plugin:4.0.0-SNAPSHOT"
//    compile "org.grails.plugins:ala-ws-plugin:4.0.0-SNAPSHOT"
//    if(!inplace) {
//        compile "org.grails.plugins:ala-auth:5.0.0-SNAPSHOT"
//    }
    compile "org.grails.plugins:ala-admin-plugin:2.2"

//    compile "org.grails.plugins:ala-bootstrap3:4.0.0-SNAPSHOT"

    compile "org.grails.plugins:quartz:2.0.13"
    implementation 'org.quartz-scheduler:quartz:2.2.1' // Is not pulled in transitively?

    compile "org.grails.plugins:mail:3.0.0"
//    compile 'org.grails.plugins:grails-java8:1.2.1'
//    compile 'org.grails.plugins:swagger:1.0.1'
    compile 'au.org.ala.plugins:openapi:1.1.0'

    compile "org.gbif:gbif-doi:2.17"

    // override log4j in elasticsearch plugin
    // TODO remove this once no log4j vulnerable transitive deps are used.
    runtime 'org.apache.logging.log4j:log4j-core:2.17.1'
    runtime 'org.apache.logging.log4j:log4j-api:2.17.1'

    // DB related
    compile group: 'org.postgresql', name: 'postgresql', version: '42.3.3'
    compile 'org.grails.plugins:postgresql-extensions:7.0.0'
    compile 'org.flywaydb:flyway-core:8.5.2'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.2'

    compile group: 'com.google.guava', name: 'guava', version: '23.2-jre'
    compile 'com.jakewharton.byteunits:byteunits:0.9.1'

    compile 'org.grails.plugins:aws-sdk-s3:2.4.8'
    compile 'org.javaswift:joss:0.10.4'
    compile 'org.jsoup:jsoup:1.11.3'

    testCompile "org.apache.httpcomponents:httpmime:4.3.3"
}

springBoot {
    buildInfo()
//    layout = 'WAR'
}

bootRun {
    ignoreExitValue true
    jvmArgs(
            '-Dspring.output.ansi.enabled=always',
            '-noverify',
            '-XX:TieredStopAtLevel=1',
            '-Xmx1024m')
    sourceResources sourceSets.main
    String springProfilesActive = 'spring.profiles.active'
    systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}

bootWar {
    launchScript()
    classifier = 'exec'
}

tasks.withType(GroovyCompile) {
    configure(groovyOptions) {
        forkOptions.jvmArgs = ['-Xmx1024m']
    }
}

tasks.withType(Test) {
    systemProperty "geb.env", System.getProperty('geb.env')
    systemProperty "geb.build.reportsDir", reporting.file("geb/integrationTest")
    systemProperty "webdriver.chrome.driver", System.getProperty('webdriver.chrome.driver')
    systemProperty "webdriver.gecko.driver", System.getProperty('webdriver.gecko.driver')
}

assets {
    minifyJs = true
    minifyCss = true
//    includes = ["fonts/*"]
}

publishing {
    repositories {
        maven {
            name 'Nexus'
            url "https://nexus.ala.org.au/content/repositories/${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases' }"
            credentials {
                username = System.getenv('TRAVIS_DEPLOY_USERNAME')
                password = System.getenv('TRAVIS_DEPLOY_PASSWORD')
            }
        }
    }
    publications {
        mavenJar(MavenPublication) {
            artifact bootWar
        }
    }
}
